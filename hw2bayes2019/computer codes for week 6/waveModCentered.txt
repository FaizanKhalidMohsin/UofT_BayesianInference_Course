
    model{
    for(i in 1:34)
    {
    #ship   yrcons   yrop  month daminc
    
    daminc[i]~dpois(lam[i])
    log(lam[i]) <- log(month[i]) + beta0 + beta.o*yrop[i] + beta.s[ship[i]]+ beta.c[yrcons[i]]  + b[i] 
    b[i] ~dnorm(0,tau)
    b.adj[i] <- b[i] - mean(b[]) 
    }
    for(is in 1:5){
    beta.s[is]~dnorm(0,tau.s)
    beta.s.adj[is] <- beta.s[is] -mean(beta.s[]) 
    }
    for(ic in 1:4){
    beta.c[ic]~dnorm(0,tau.c)
    beta.c.adj[ic] <- beta.c[ic] - mean(beta.c[])
    }
    
    # Using tigher priors
    #  Note: total ship-years per categories is less than 50,000
    #  ln(50000) ~ 2.3*4 + 1.6 = 10.8....
    #  so rate has to be bigger than 1/50,000 and log(rate) > -10.8... 
    #  so log( base rate) should be between about -11 and 11.
    # 1/11/11 is about .0082
    beta0 ~ dnorm(0, .0082)  
    beta0.adj <- beta0 + mean(b[]) + mean(beta.s[])+ mean(beta.c[])
    
    
    # for the <extra poisson variation> ... 
    # assume bounded by very big number... say 1000 times... 
    # so log(1000) is about 2.3*4 which is about 9.2
    std ~ dunif(0, 9)
    tau <- 1/std/std
    
    # for the relative risk between groups... a very large number would be 100 times, 
    #  so, log(100) is about 2.3*2 or about 4.6
    #  also, note that 1/5/5 is 0.04
    #  
    std.s ~dunif(0, 5)
    tau.s <- 1/std.s/std.s
    std.c ~ dunif(0,5) 
    tau.c <- 1/std.c/std.c
    
    beta.o~dnorm(0, .04)
    
    }